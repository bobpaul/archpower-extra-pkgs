# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=yasm
pkgver=1.3.0
pkgrel=7
pkgdesc="A rewrite of NASM to allow for multiple syntax supported (NASM, TASM, GAS, etc.)"
arch=('x86_64' 'powerpc' 'powerpc64' 'powerpc64le')
url="https://www.tortall.net/projects/yasm/"
license=('BSD' 'GPL2' 'LGPL2.1' 'PerlArtistic')
depends=('glibc')
checkdepends=('systemd')
options=('staticlibs')
source=(https://www.tortall.net/projects/yasm/releases/${pkgname}-${pkgver}.tar.gz)
sha256sums=('3dce6601b495f5b3d45b59f7d2492a340ee7e84b5beca17e48f862502bd5603f')

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=' -ffat-lto-objects' \
  cmake \
    -B "_build" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_INSTALL_LIBDIR="lib" \
    -DBUILD_SHARED_LIBS=OFF \
    ./
  make -C "_build"
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 -C "_build" test
}

package() {
  cd ${pkgname}-${pkgver}

  make -C "_build" DESTDIR="$pkgdir" install
  install -Dm644 {BSD.txt,COPYING} -t "$pkgdir/usr/share/licenses/yasm"
}
